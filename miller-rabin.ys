# Calling Conventions

# Arguments:
#     multiply(a, b)                 : %rsi = a, %rdi = b
#     halve(dividend)                : %rsi = dividend
#     fast_modulo(dividend, modulo)  : %rsi = dividend, %rdi =  modulo
#     modpow(base, exponent, modulo) : %rsi = base, %rbx = exponent, %rdi = modulo

# Return values: %rax

# Scratch / Caller-Saved: %rax, %r8 - 14

# Globals / Callee-Saved: %rcx, %rdx, %rbx

#.pos 0
#    irmovq    stack, %rbp
#    irmovq    stack, %rsp
#    irmovq $0x1800, %r8            # array start at %r8

test:
    #irmovq    $666, %rsi
    #irmovq    $2, %rdi
    #call      multiply
    #rrmovq    %rax, %rsi
    #call      halve
    #rrmovq    %rax, %rsi
    #irmovq    $77, %rdi
    #call      fast_modulo
    #irmovq     $22, %rsi          # BASE
    #irmovq     $666, %rbx         # EXPONENT
    #irmovq     $15, %rdi          # MODULO
    #call       modpow

# Main program using:
#    %rcx    :    pointer to input array
#    %rdx    :    pointer to witness array
#    %rsi    :    witness
#    %rdi    :    candidate
main:
    rrmovq    %r8, %rcx                  # move input array pointer to %rxc
    irmovq    $0x1000, %rdx              # witness array pointer to %rdx

# Outer loop iterates through the input array and validates the values
input_loop:
    xorq      %rax, %rax                 # init return value
    mrmovq    0(%rcx), %rdi              # move current prime candidate to %rdi
    andq      %rdi, %rdi                 # check for zero
    je        done                       # end of array, no PRIMES -> done
    
    # Update pointers
    irmovq    $8, %rax                   # constant 8 to %rax
    addq      %rax, %rcx                 # increment input pointer by 8
    irmovq    witnesses, %rdx            # reset witness array pointer

    # Check if the candidate is even or 1 or 2
    rrmovq    %rdi, %r8                # move temp candidate to %r8
    irmovq    $1, %r9                  # const 1 to %r9
    andq      %r9, %r8                 # %r8 AND &r9
    je        input_loop               # Even -> COMPOUND
    rrmovq    %rdi, %r8                # move temp candidate to %r8
    subq      %r9, %r8                 # compare to 1
    je        input_loop               # candidate was 1, not a PRIME
    subq      %r9, %r8                 # subtract 1 again, goes to zero if 2
    je        prime_found              # found PRIME 2

# Inner loop tests a possible prime against witnesses
witness_loop:
    mrmovq    0(%rdx), %rsi            # move current witness to %rsi
    andq      %rsi, %rsi               # at the end of witness array
    je        prime_found              # no more witnesses -> PRIME found
    
    # Advance witness array pointer
    irmovq    $8, %rax                 # const 8 to %rax
    addq      %rax, %rdx               # increment witness pointer by 8
    
    # Check if number >= witness
    rrmovq    %rdi, %r8                # candidate temp to %r8
    subq      %rsi, %r8                # compare to witness
    cmovle    %rdi, %rax               # found a PRIME, return to %rax
    jle       witness_loop             # no more valid witnesses -> PRIME
    
    # Save main loop state
    pushq     %rcx                  # input pointer to stack
    pushq     %rdx                  # witness pointer to stack
    
    call      miller_rabin_round    # miller_rabin_round(candidate, witness)

    popq      %rdx                  # pop witness pointer from stack
    popq      %rcx                  # pop input pointer from stack
    
    andq      %rax, %rax            # check round result
    je        input_loop            # COMPOUND if zero, next candidate
    
    jmp       witness_loop               # iterate over witnesses

prime_found:
    rrmovq    %rdi, %rax            # move PRIME to %rax for return

done:
    halt

# Subroutine miller_rabin(long candidate, long witness)
# candidate in %rsi
# witness in % rdi
# Using:
#    %rsi = witness
#    %rdi = candidate
#    %rbx = p
#    %rdx = candidate - 1
#    %rcx = const 1
#    The ordering makes calling modpow easy since the witness
#    will stay nicely in the right register and only p is 
#    modified by halving when iterating!
# clobbers: %rax, %rbx, %rcx, %rdx, %rbx, %r8 - 14
miller_rabin_round:
    # Save arguments unnecessary?
    #pushq     %rsi                      # save candidate
    #pushq     %rdi                      # save witness

    irmovq    $1, %rcx               # init const 1 to %rcx
    rrmovq    %rdi, %rdx             # init candidate - 1 to %rdx
    subq      %rcx, %rdx
    rrmovq    %rdx, %rbx             # init p = candidate - 1 to %rbx

    # check if modpow(witness, p, number) != 1
    call      modpow            # modpow(witness, p, candidate)
    subq      %rcx, %rax        # compare result to 1
    jne       compound         # found a COMPOUND
    
p_halving_loop:
    # Check if (p AND 1)
    rrmovq    %rbx, %r8         # temp of p to %r8
    andq      %rcx, %r8         # p AND 1
    jne       prime             # p was odd -> PRIME
    
    # Halve p and recheck
    call      halve             # halves the p in %rbx
    call      modpow            # modpow with the halved p
    # Check if (result == 1)
    rrmovq    %rax, %r8         # result temp to %r8
    subq      %rcx, %r8         # compare to const 1
    je        p_halving_loop    # continue
    # Check if (result == number - 1)
    rrmovq    %rax, %r8         # result temp to %r8
    subq      %rdx, %r8         # compare to candidate - 1
    je        prime             # found PRIME
    # Else
    jmp       compound          # found COMPOUND


compound:
    irmovq    $0, %rax          # COMPOUND -> return 0
    jmp       miller_rabin_done

prime:
    rrmovq    %rcx, %rax        # PRIME -> return const 1

miller_rabin_done:
    # Reset arguments
    #popq      %rdi                      # reset witness
    #popq      %rsi                      # reset candidate
    ret

# Subroutine multiply(long a, long b)
# a in %rsi
# b in %rdi
# return value in %rax
# clobbers: %rax, %r8, %r9, %r10
multiply:
    # Early return zero checks could/should be removed if they aren't needed!
    andq       %rsi, %rsi                    # Zero check operands for early return
    je         multiply_done
    andq       %rdi, %rdi                    # %rdi = the shifted operarand
    je         multiply_done
    irmovq     $0x1, %r8                     # init bit mask in %rbx
    rrmovq     %rdi, %r10                    # init mutable temp of the second operand
    xorq       %rax, %rax                    # init accumulator %rax = 0

multiply_loop:
    rrmovq    %rsi, %r9                     # temp of operand
    subq      %r8, %r9                      # Check for relevant bits lef
    jl        multiply_done                  # No more terms
    rrmovq    %rsi, %r9                     # temp of operand
    andq      %r8, %r9                      # Use mask to check current term
    je        do_them_shifts                 # A term was 0 -> iterate
    addq      %r10, %rax                     # result += current shift of the operand
    
do_them_shifts:
    addq      %r8, %r8                     # Left shift mask  
    addq      %r10, %r10                     # Left shift operand
    jmp       multiply_loop
    
multiply_done:
    ret                                      # Return result in %rax

# Subroutine halve(long dividend)
# dividend in %rbx (%rbx holds p during the miller_rabin_round!)
# return value in %rax
# clobbers: %rax, %rbx %r8, %r9, %r10 (side effects in %rbx!)
halve:
    irmovq    $0x2, %r8                  # init single bit mask in at 0b10
    irmovq    $1, %r9                    # init cursor in %r9 at 1
    xorq      %rax, %rax                 # init accumulator = 0
    
halve_loop:
    rrmovq    %rbx, %r10                 # tmp of the dividend
    subq      %r8, %r10                  # check if any bits remain for processing
    jl        halve_done                 # no more bits to process
    rrmovq    %rbx, %r10                 # tmp of the dividend
    andq      %r8, %r10                  # check current bit
    je        update_halve_parameters    # current term = 0 -> iterate
    addq      %r9, %rax                  # add cursor to accumulator
    
update_halve_parameters:
   rrmovq     %r8, %r9                   # make old mask the new cursor
   addq       %r8, %r8                   # shift the mask
   jmp        halve_loop                 # loopidy loop
    
halve_done:
    rrmovq    %rax, %rbx                 # result to %rbx for halving p
    ret

# Subroutine fast_modulo(long dividend, long modulo)
# dividend in %rsi
# modulo in %rdi
# clobbers: %rax, %r8, %r9
fast_modulo:
    rrmovq    %rsi, %rax      # store the result in accumulator %rax
    rrmovq    %rsi, %r9       # Copy the dividend       
    subq      %rdi, %r9       # check for early return
    jl        modulo_done     # divisor larger than dividend -> do nothing
    rrmovq    %rdi, %r8       # %r8 = mutable divisor copy for doubling
    
modulo_loop:
    rrmovq    %rax, %r9       # Temp of result
    subq      %r8, %r9        # check if goes to zero or less
    cmovge    %r9, %rax       # update modulo if zero or above
    jle       modulo_done     # result was zero or below -> done

update_divisor:
    addq      %r8, %r8        # double the divisor (might need to quadruple this!)
    addq      %r8, %r8        # quadruple!
    rrmovq    %r8, %r9        # a temp for testing
    xorq      %rdi, %r9       # flips OF if the sign changed (should go negative only if the MSB differs)
    jl        reset_divisor
    rrmovq    %r8, %r9        # a temp for testing
    subq      %rax, %r9       # check if divisor is larger than current dividend
    jg        reset_divisor
    jmp       modulo_loop
    
reset_divisor:
    rrmovq    %rdi, %r8       # reset original divisor from %r9
    jmp       modulo_loop

modulo_done:
    ret

# Subroutine modpow(long base, long, exponent, long modulo)
# base in %rsi
# exponent in %rbx (immutable!)
# modulo in %rdi

# using:     %r14: result accumulator
#            %r13: bit mask
#            %r12: modulo
#            %r11: mutable base

# return value in %rax
# clobbers %rax, %r8-14
modpow:
    # Save copies of arguments to prevent clobbering by subroutines
    pushq     %rsi            # save unmodified base on the stack
    rrmovq    %rsi, %r11      # base to %r11
    rrmovq    %rdi, %r12      # modulo to %r12
    
    # Calculate and assign base := base % modulo
    call      fast_modulo     # arguments already set as fast_modulo(base, modulo)
    rrmovq    %rax, %r11      # init base
    
    # Start from result = 1
    irmovq    $1, %r14        # %r14 = Result accumulator
    
    # Initialize masks
    irmovq    $0x1, %r13          # init first bit mask
    
modpow_loop:
    # Iterate for every relevant bit in the exponent
    rrmovq    %rbx, %rax            # Tmp of exponent
    subq      %r13, %rax            # Check for relevant bits left with mask
    jl        modpow_done           # No more bits to process
    rrmovq    %rbx, %rax            # Tmp of exponent
    andq      %r13, %rax            # Check if current bit is set
    je        modpow_update_params  # Current bit not set, skip to iteration end
    
    # If bit is set: result := result * base % modulo
    rrmovq    %r14, %rsi            # 1. argument of multiply = result
    rrmovq    %r11, %rdi            # 2. argument of multiply = base
    call      multiply              # multiply(result, base)
    rrmovq    %rax, %rsi            # Set result as 1. argument of fast_modulo
    rrmovq    %r12, %rdi            # 2. argument of fast_modulo = modulo
    call      fast_modulo           # fast_modulo(result, modulo)
    rrmovq    %rax, %r14            # Update main result
 
modpow_update_params:
    # Shift iteration masks
    addq      %r13, %r13     # Left shift first bit mask
    
    # base := base * base % modulo
    rrmovq    %r11, %rsi      # 1. argument of multiply = base
    rrmovq    %r11, %rdi      # 1. argument of multiply = base
    call      multiply        # multiply(base, base)
    rrmovq    %rax, %rsi      # 1. argument of fast_modulo = base * base
    rrmovq    %r12, %rdi      # 2. argument of fast_modulo = modulo
    call      fast_modulo     # (base * base) % modulo
    rrmovq    %rax, %r11       # Update base
    jmp       modpow_loop
    
modpow_done:
    # restore modified argument values
    popq      %rsi           # restore base from stack
    rrmovq    %r12, %rdi     # restore modulo
    
    rrmovq    %r14, %rax     # Return result in %rax
    ret

.pos 0x400
stack:

.pos 0x1000
witnesses:
# 2, 3, 5, 7, 11, 13, 17, 19, 23, 29, 31, 37
.align 8
.quad 2
.quad 3
.quad 5
.quad 7
.quad 11
.quad 13
.quad 17
.quad 19
.quad 23
.quad 29
.quad 31
.quad 37

#.pos 0x1800
#input_array:
#.align 8
##.quad 1 # not prime!
#.quad 2 # even
##.quad 9 # not prime
##.quad 1
#.quad 0
